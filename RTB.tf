# Public route table -> IGW
resource "aws_route_table" "public" {
  vpc_id = aws_vpc.classic_vpc.id

  route {
    cidr_block = "0.0.0.0/0"
    gateway_id = aws_internet_gateway.igw.id
  }

  tags = { Name = "classic-rt-public" }
}
resource "aws_route_table_association" "public" {
  count          = length(var.azs)
  subnet_id      = aws_subnet.public[count.index].id
  route_table_id = aws_route_table.public.id
}

# Private APP route table -> NAT
resource "aws_route_table" "private_app" {
  vpc_id = aws_vpc.classic_vpc.id

  route {
    cidr_block     = "0.0.0.0/0"
    nat_gateway_id = aws_nat_gateway.nat.id
  }

  tags = { Name = "classic-rt-private-app" }
}

resource "aws_route_table_association" "private_app" {
  count          = length(var.azs)
  subnet_id      = aws_subnet.private_app[count.index].id
  route_table_id = aws_route_table.private_app.id
}

# Private DB route table (NO default route to internet)
resource "aws_route_table" "private_db" {
  vpc_id = aws_vpc.classic_vpc.id
  tags   = { Name = "classic-rt-private-db" }
}

resource "aws_route_table_association" "private_db" {
  count          = length(var.azs)
  subnet_id      = aws_subnet.private_db[count.index].id
  route_table_id = aws_route_table.private_db.id
}